(**
* Управление краном Gidrolock
* @пл13 - выход датчика, показывает, что кран открыт
* @пл14 - выход датчика, показывает, что кран закрыт
* если XOR(пл10, пл11), то состояние не определено
* Связь:
* @пл21 - рв7
* @пл12 - рв8 управление краном (1 - открыт (белый);0 - закрыт(зелено-желтый))
* @рв9 - состояние крана
* @пл10 - смена команды крана
**)

ПРОЦЕДУРА Gidrolock_InitOnReboot
	НАЧАЛО
		ПЕРЕМЕННЫЕ
			НАЧАЛО
				ЛОГ openCrane
				ЛОГ closeCrane
			КОНЕЦ
			
		ДЛЯ iMdbGidrolockCraneWireOpen[13],
			iMdbGidrolockCraneWireClose[14],
			iOpcGidrolockCraneCmd[8],
			iMdbCraneId[12]
		{
			openCrane = пл[iMdbGidrolockCraneWireOpen]
			closeCrane = пл[iMdbGidrolockCraneWireClose]
			рв[iOpcGidrolockCraneCmd] = STATE_GIDROLOCK_CRANE_AWAIT
		}
	КОНЕЦ
	

ПРОЦЕДУРА Gidrolock_OpcControl
	НАЧАЛО
		ПЕРЕМЕННЫЕ
			НАЧАЛО
				ЦЕЛ16 opcCraneControlCmd
			КОНЕЦ
		КОНСТАНТЫ
			НАЧАЛО
				ЛОГ IS_OPEN_CRANE = 1
			КОНЕЦ
	
		ДЛЯ iOpcGidrolockCraneCmd[8],
			iOpcGidrolockCraneState[9],
			iMdbCraneId[12]
		{		
			opcCraneControlCmd = вц(рв[iOpcGidrolockCraneCmd])
			
			ЕСЛИ (opcCraneControlCmd = STATE_GIDROLOCK_CRANE_OPEN){
				ЕСЛИ (рв[iOpcGidrolockCraneState] = STATE_GIDROLOCK_CRANE_CLOSE){
					пл[iMdbCraneId] = IS_OPEN_CRANE
				}			
			}
			ИНАЧЕ_ЕСЛИ (opcCraneControlCmd = STATE_GIDROLOCK_CRANE_CLOSE){
				ЕСЛИ (рв[iOpcGidrolockCraneState] = STATE_GIDROLOCK_CRANE_OPEN){	
					пл[iMdbCraneId] = !IS_OPEN_CRANE
				}
			}
		}
		
	КОНЕЦ
	
ПРОЦЕДУРА Gidrolock_Monitoring
	НАЧАЛО
		ПЕРЕМЕННЫЕ
			НАЧАЛО
				ЛОГ openCrane
				ЛОГ closeCrane
				ЦЕЛ16 craneState
			КОНЕЦ
							
		ДЛЯ iMdbGidrolockCraneWireOpen[13],
			iMdbGidrolockCraneWireClose[14],
			iOpcGidrolockCraneState[9],
			iOpcGidrolockCraneCmd[8],
			iMdbCraneId[12],
			iTmrTsCraneChangeState[5],
			iTmrTsCraneDelayChangeState[6],
			iCraneCmdChange[10]
		{		
			openCrane = пл[iMdbGidrolockCraneWireOpen]
			closeCrane = пл[iMdbGidrolockCraneWireClose]
			craneState = Decoder_GetGidrolockCraneState(openCrane, closeCrane, пл[iMdbCraneId])
		
			:* true, если крану дана какая-то команда
			ЕСЛИ (рв[iOpcGidrolockCraneCmd] # STATE_GIDROLOCK_CRANE_AWAIT) {
				:* true, если кран находится в состоянии, перейти в которое ему указывает команда
				ЕСЛИ (рв[iOpcGidrolockCraneCmd] = цв(craneState)){
					выкл тс[iTmrTsCraneChangeState]
					тс[iTmrTsCraneChangeState] = 0
					
					тс[iTmrTsCraneDelayChangeState] = 0
					выкл тс[iTmrTsCraneDelayChangeState]
					
					:* Переводим кран в режим ожидания
					рв[iOpcGidrolockCraneCmd] = STATE_GIDROLOCK_CRANE_AWAIT
				}
				ИНАЧЕ {
					:* true, если команда указывает перейти из состояния открыт(закрыт) в закрыт(открыт) и состояние еще не достигнуто
					ЕСЛИ ((craneState = STATE_GIDROLOCK_CRANE_OPEN | craneState = STATE_GIDROLOCK_CRANE_CLOSE) & тс[iTmrTsCraneDelayChangeState] = 0){
						:* Запускаем таймер, отсчитывающий время задержки между получением команды и началом её выполнения
						тс[iTmrTsCraneDelayChangeState] = 0
						вкл тс[iTmrTsCraneDelayChangeState]
						:* Запускаем таймер, отсчитывающий время переключения между конечными состояниями
						тс[iTmrTsCraneChangeState] = 0
						вкл тс[iTmrTsCraneChangeState]
					}
					ИНАЧЕ_ЕСЛИ (craneState = STATE_GIDROLOCK_CRANE_OPENS | craneState = STATE_GIDROLOCK_CRANE_CLOSES){
						тс[iTmrTsCraneDelayChangeState] = 0
						выкл тс[iTmrTsCraneDelayChangeState]
					}
				}
			}
			рв[iOpcGidrolockCraneState] = цв(Decoder_GetGidrolockCraneFullState(craneState, вц(тс[iTmrTsCraneChangeState]), вц(тс[iTmrTsCraneDelayChangeState])))
		}
	КОНЕЦ

(*********************************************************************)

ПРОЦЕДУРА Gidrolock_Maintenance
	НАЧАЛО
		ПЕРЕМЕННЫЕ
			НАЧАЛО
				ЦЕЛ16 day
				ЦЕЛ16 hour
				ЦЕЛ16 minute
				ЦЕЛ16 second
			КОНЕЦ
		(,,day) = СДата()
		(hour, minute, second) = СВремя()
		ЕСЛИ ((day = 1 | day = 16) & (hour = 0 & minute = 0 & (second = 0 | second = 1))){
			ДЛЯ iGidrolockCraneId[1]
			{
				ЕСЛИ (Gidrolock_GetStateById(iGidrolockCraneId) = STATE_GIDROLOCK_CRANE_CLOSE){
					рв28 = 566
					Gidrolock_CmdOpen(iGidrolockCraneId)		
				}
				ИНАЧЕ {
					рв28 = 567
					Gidrolock_CmdClose(iGidrolockCraneId)
				}
			}
		}
	КОНЕЦ
		

(*********************************************************************)

ФУНКЦИЯ Gidrolock_CmdClose
	ВХОДНЫЕ_ПЕРЕМЕННЫЕ
		НАЧАЛО
			ЦЕЛ16 gidrolockCraneId
		КОНЕЦ
	НАЧАЛО
		ДЛЯ iGidrolockCraneId[1],
			iOpcGidrolockCraneCmd[8],
			iOpcGidrolockCraneState[9],
			iMdbCraneId[12],
			iCraneCmdChange[10]
		{
			ЕСЛИ (iGidrolockCraneId = gidrolockCraneId)	{
				ЕСЛИ (рв[iOpcGidrolockCraneCmd] = CMD_GIDROLOCK_CRANE_OPEN){
					пл[iMdbCraneId]	= CMD_GIDROLOCK_CRANE_CLOSE
					рв[iOpcGidrolockCraneCmd] = CMD_GIDROLOCK_CRANE_CLOSE
				}
				ПРЕРВАТЬ
			}
		}
	КОНЕЦ

(*********************************************************************)
ФУНКЦИЯ Gidrolock_CmdOpen
	ВХОДНЫЕ_ПЕРЕМЕННЫЕ
		НАЧАЛО
			ЦЕЛ16 gidrolockCraneId
		КОНЕЦ
	НАЧАЛО
		ДЛЯ iGidrolockCraneId[1],
			iMdbCraneId[12],
			iOpcGidrolockCraneCmd[8],
			iOpcGidrolockCraneState[9],
			iCraneCmdChange[10]
		{
			ЕСЛИ (iGidrolockCraneId = gidrolockCraneId){
				ЕСЛИ (рв[iOpcGidrolockCraneState] = STATE_GIDROLOCK_CRANE_CLOSE){ 
					пл[iMdbCraneId]	= CMD_GIDROLOCK_CRANE_OPEN
					рв[iOpcGidrolockCraneCmd] = CMD_GIDROLOCK_CRANE_OPEN
				}
				ПРЕРВАТЬ
			}
		}
	КОНЕЦ

(*********************************************************************)

ФУНКЦИЯ Gidrolock_GetStateById
	ВХОДНЫЕ_ПЕРЕМЕННЫЕ
		НАЧАЛО
			ЦЕЛ16 gidrolockCraneId
		КОНЕЦ
	ВЫХОДНЫЕ_ПЕРЕМЕННЫЕ
		НАЧАЛО
			ЦЕЛ16 gidrolockCraneState
		КОНЕЦ
	
	НАЧАЛО
		ДЛЯ iGidrolockCraneId[1],
			iOpcGidrolockCraneState[9]
		{
			ЕСЛИ (iGidrolockCraneId = gidrolockCraneId){
				gidrolockCraneState = вц(рв[iOpcGidrolockCraneState])
				ПРЕРВАТЬ
			}
		}
	КОНЕЦ

(*********************************************************************)

ФУНКЦИЯ Gidrolock_IsClosed
	ВХОДНЫЕ_ПЕРЕМЕННЫЕ
		НАЧАЛО
			ЦЕЛ16 gidrolockCraneId
		КОНЕЦ
	ВЫХОДНЫЕ_ПЕРЕМЕННЫЕ
		НАЧАЛО
			ЛОГ isClosedCrane
		КОНЕЦ
	
	НАЧАЛО
		ПЕРЕМЕННЫЕ
			НАЧАЛО
				ЦЕЛ16 craneState
			КОНЕЦ
		ДЛЯ iGidrolockCraneId[1],
			iMdbCraneOpenWire[13],
			iMdbCraneId[12],		
			iMdbCraneCloseWire[14]
		{
			ЕСЛИ (iGidrolockCraneId = gidrolockCraneId){
				craneState = Decoder_GetGidrolockCraneState(пл[iMdbCraneOpenWire], пл[iMdbCraneCloseWire], пл[iMdbCraneId])
				isClosedCrane = (craneState = STATE_GIDROLOCK_CRANE_CLOSE)
				ПРЕРВАТЬ
			}
		}
	КОНЕЦ

(*********************************************************************)

ФУНКЦИЯ Gidrolock_IsOpened 
	ВХОДНЫЕ_ПЕРЕМЕННЫЕ
		НАЧАЛО
			ЦЕЛ16 gidrolockCraneId
		КОНЕЦ
	ВЫХОДНЫЕ_ПЕРЕМЕННЫЕ
		НАЧАЛО
			ЛОГ isOpenedCrane
		КОНЕЦ
	
	НАЧАЛО
		ПЕРЕМЕННЫЕ
			НАЧАЛО
				ЦЕЛ16 craneState 
			КОНЕЦ
		ДЛЯ iGidrolockCraneId[1],
			iMdbCraneOpenWire[13],
			iMdbCraneId[12],
			iMdbCraneCloseWire[14]
		{
			ЕСЛИ (iGidrolockCraneId = gidrolockCraneId){
				craneState = Decoder_GetGidrolockCraneState(пл[iMdbCraneOpenWire], пл[iMdbCraneCloseWire], пл[iMdbCraneId])
				isOpenedCrane = (craneState = STATE_GIDROLOCK_CRANE_OPEN)
				ПРЕРВАТЬ
			}
		}
	КОНЕЦ