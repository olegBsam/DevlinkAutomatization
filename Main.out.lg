Программа Main
Начало
  Константы
    Начало
    Конец
  Переменные
    Начало
    Конец
  Если (рв1 = 0.000000)
  {
    Initialization_OnReboot
    рв1 = 1.00000
  }
  
  Если (рв26 = 1.00000)
  {
    Если (рв8 # 6.00000)
    {
      пл13 = 1
      пл14 = 0
    }
    
  }
  
  AlgorithmCraneMaintenance_Gidrolock_Maintenance
  Msu24_Monitoring
  Gidrolock_Monitoring
  Gidrolock_OpcControl
  Если (рв25 = 1.00000)
  {
    Gidrolock_CmdClose(1)
  }
  Иначе
  {
    Если (рв25 = 11.0000)
    {
      Gidrolock_CmdOpen(1)
    }
    
  }
  
Конец

Процедура AlgorithmCraneMaintenance_Gidrolock_Maintenance
Начало
  Константы
    Начало
    Конец
  Переменные
    Начало
      Цел16 clearingDay1
      Цел16 clearingDay2
      Цел16 clearingHour
    Конец
  clearingDay1 = 25
  clearingDay2 = 16
  clearingHour = 18
  Для iGidrolockCraneId[1, 2], iOpcClearingSt[28, 29], iClearingMinute[42, 43], iClearingSecond[0, 0]
  {
    рв[iOpcClearingSt] = _Gidrolock_ClearingAlgorithm(рв[iOpcClearingSt], iGidrolockCraneId, clearingDay1, clearingDay2, clearingHour, iClearingMinute, iClearingSecond)
  }
  
Конец

Процедура Gidrolock_InitOnReboot
Начало
  Константы
    Начало
    Конец
  Переменные
    Начало
      Лог openCrane
      Лог closeCrane
    Конец
  Для iMdbGidrolockCraneWireOpen[13, 16], iMdbGidrolockCraneWireClose[14, 17], iOpcGidrolockCraneCmd[8, 10], iMdbCraneId[12, 15]
  {
    openCrane = пл[iMdbGidrolockCraneWireOpen]
    closeCrane = пл[iMdbGidrolockCraneWireClose]
    рв[iOpcGidrolockCraneCmd] = 6.00000
  }
  
Конец

Процедура Gidrolock_OpcControl
Начало
  Константы
    Начало
      Лог IS_OPEN_CRANE = 1
    Конец
  Переменные
    Начало
      Цел16 opcCraneControlCmd
    Конец
  Для iOpcGidrolockCraneCmd[8, 10], iOpcGidrolockCraneState[9, 11], iMdbCraneId[12, 15]
  {
    opcCraneControlCmd = вц(рв[iOpcGidrolockCraneCmd])
    Если (opcCraneControlCmd = 1)
    {
      Если (рв[iOpcGidrolockCraneState] = 0.000000)
      {
        пл[iMdbCraneId] = 1
      }
      
    }
    Иначе
    {
      Если (opcCraneControlCmd = 0)
      {
        Если (рв[iOpcGidrolockCraneState] = 1.00000)
        {
          пл[iMdbCraneId] = 0
        }
        
      }
      
    }
    
  }
  
Конец

Процедура Gidrolock_Monitoring
Начало
  Константы
    Начало
    Конец
  Переменные
    Начало
      Лог isCraneSwitched
      Лог openCrane
      Лог closeCrane
      Цел16 craneState
    Конец
  Для iMdbGidrolockCraneWireOpen[13, 16], iMdbGidrolockCraneWireClose[14, 17], iOpcGidrolockCraneState[9, 11], iOpcGidrolockCraneCmd[8, 10], iMdbCraneId[12, 15], iTmrTsCraneChangeState[5, 7], iTmrTsCraneDelayChangeState[6, 8]
  {
    openCrane = пл[iMdbGidrolockCraneWireOpen]
    closeCrane = пл[iMdbGidrolockCraneWireClose]
    craneState = Decoder_GetGidrolockCraneState(openCrane, closeCrane, пл[iMdbCraneId])
    Если (рв[iOpcGidrolockCraneCmd] # 6.00000)
    {
      isCraneSwitched = ((рв[iOpcGidrolockCraneCmd] = 0.000000) & (craneState = 0))
      isCraneSwitched = (isCraneSwitched | ((рв[iOpcGidrolockCraneCmd] = 1.00000) & (craneState = 1)))
      Если isCraneSwitched
      {
        Выкл тс[iTmrTsCraneChangeState]
        тс[iTmrTsCraneChangeState] = 0.000000
        тс[iTmrTsCraneDelayChangeState] = 0.000000
        Выкл тс[iTmrTsCraneDelayChangeState]
        рв[iOpcGidrolockCraneCmd] = 6.00000
      }
      Иначе
      {
        Если (((craneState = 1) | (craneState = 0)) & (тс[iTmrTsCraneDelayChangeState] = 0.000000))
        {
          тс[iTmrTsCraneDelayChangeState] = 0.000000
          Вкл тс[iTmrTsCraneDelayChangeState]
          тс[iTmrTsCraneChangeState] = 0.000000
          Вкл тс[iTmrTsCraneChangeState]
        }
        Иначе
        {
          Если ((craneState = 5) | (craneState = 4))
          {
            тс[iTmrTsCraneDelayChangeState] = 0.000000
            Выкл тс[iTmrTsCraneDelayChangeState]
          }
          
        }
        
      }
      
    }
    
    рв[iOpcGidrolockCraneState] = цв(Decoder_GetGidrolockCraneFullState(craneState, вц(тс[iTmrTsCraneChangeState]), вц(тс[iTmrTsCraneDelayChangeState])))
  }
  
Конец

Процедура Initialization_OnReboot
Начало
  Константы
    Начало
    Конец
  Переменные
    Начало
    Конец
  Gidrolock_InitOnReboot
Конец

Процедура Msu24_Monitoring
Начало
  Константы
    Начало
    Конец
  Переменные
    Начало
    Конец
  Для iMdbPcSensorValue[10...12], iOpcSensorValue[3...5]
  {
    рв[iOpcSensorValue] = цв(пц[iMdbPcSensorValue])
  }
  
  Для iOpcIsConnectionMsu24[6], iMdbIsConnectionMsu24[20]
  {
    рв[iOpcIsConnectionMsu24] = лв(пл[iMdbIsConnectionMsu24])
  }
  
Конец

Функция _Gidrolock_ClearingAlgorithm
  Входные_Переменные
    Начало
      Вещ32 lastMaintenanceState
      Цел16 iGidrolockCraneId
      Цел16 day1
      Цел16 day2
      Цел16 hour
      Цел16 minute
      Цел16 second
    Конец
  Выходные_Переменные
    Начало
      Вещ32 maintenanceState
    Конец
Начало
  Константы
    Начало
    Конец
  Переменные
    Начало
      Цел16 sysDay
      Цел16 sysHour
      Цел16 sysMinute
      Цел16 sysSecond
      Цел16 craneState
    Конец
  maintenanceState = lastMaintenanceState
  (, , sysDay) = СДата()
  Если ((sysDay = day1) | (sysDay = day2))
  {
    (sysHour, sysMinute, sysSecond) = СВремя()
    Если (((sysHour = hour) & (sysMinute = minute)) & (sysSecond = (second + 1)))
    {
      craneState = Gidrolock_GetStateById(iGidrolockCraneId)
      maintenanceState = _Gidrolock_MaintenanceControl(lastMaintenanceState, craneState, iGidrolockCraneId)
    }
    Иначе
    {
      Если (((sysHour = hour) & (sysMinute = minute)) & (sysSecond = second))
      {
        maintenanceState = 0.000000
      }
      
    }
    
  }
  
Конец

Функция _Gidrolock_MaintenanceControl
  Входные_Переменные
    Начало
      Вещ32 lastMaintenanceState
      Цел16 craneState
      Цел16 iGidrolockCraneId
    Конец
  Выходные_Переменные
    Начало
      Вещ32 maintenanceState
    Конец
Начало
  Константы
    Начало
    Конец
  Переменные
    Начало
    Конец
  maintenanceState = lastMaintenanceState
  Если (lastMaintenanceState = 0.000000)
  {
    Если Gidrolock_IsOpened(iGidrolockCraneId)
    {
      Gidrolock_CmdClose(iGidrolockCraneId)
      maintenanceState = 1.00000
    }
    Иначе
    {
      Если Gidrolock_IsClosed(iGidrolockCraneId)
      {
        Gidrolock_CmdOpen(iGidrolockCraneId)
        maintenanceState = 1.00000
      }
      
    }
    
  }
  Иначе
  {
    Если (lastMaintenanceState = 1.00000)
    {
      Если ((craneState = 5) | Gidrolock_IsErrorInOpens(iGidrolockCraneId))
      {
        Gidrolock_CmdClose(iGidrolockCraneId)
        maintenanceState = 0.000000
      }
      Иначе
      {
        Если ((craneState = 4) | Gidrolock_IsErrorInCloses(iGidrolockCraneId))
        {
          Gidrolock_CmdOpen(iGidrolockCraneId)
          maintenanceState = 0.000000
        }
        
      }
      
    }
    
  }
  
Конец

Функция Decoder_GetGidrolockCraneFullState
  Входные_Переменные
    Начало
      Цел16 craneState
      Цел16 craneSwitchTmr
      Цел16 craneDelaySwitchTmr
    Конец
  Выходные_Переменные
    Начало
      Цел16 realCraneState
    Конец
Начало
  Константы
    Начало
    Конец
  Переменные
    Начало
    Конец
  Если ((craneSwitchTmr <= 10) & (craneDelaySwitchTmr < 1))
  {
    realCraneState = craneState
  }
  Иначе
  {
    realCraneState = 3
  }
  
Конец

Функция Decoder_GetGidrolockCraneState
  Входные_Переменные
    Начало
      Лог isWireOpen
      Лог isWireClose
      Лог currentCmd
    Конец
  Выходные_Переменные
    Начало
      Цел16 craneState
    Конец
Начало
  Константы
    Начало
    Конец
  Переменные
    Начало
    Конец
  Если (isWireOpen & (!isWireClose))
  {
    craneState = 1
  }
  Иначе
  {
    Если ((!isWireOpen) & isWireClose)
    {
      craneState = 0
    }
    Иначе
    {
      Если (currentCmd = 1)
      {
        craneState = 5
      }
      Иначе
      {
        Если (currentCmd = 0)
        {
          craneState = 4
        }
        
      }
      
    }
    
  }
  
Конец

Функция Gidrolock_CmdClose
  Входные_Переменные
    Начало
      Цел16 gidrolockCraneId
    Конец
  Выходные_Переменные
    Начало
    Конец
Начало
  Константы
    Начало
    Конец
  Переменные
    Начало
    Конец
  Для iGidrolockCraneId[1, 2], iOpcGidrolockCraneCmd[8, 10], iOpcGidrolockCraneState[9, 11], iMdbCraneId[12, 15]
  {
    Если (iGidrolockCraneId = gidrolockCraneId)
    {
      Если ((рв[iOpcGidrolockCraneState] = 1.00000) | (рв[iOpcGidrolockCraneState] = 5.00000))
      {
        рв[iOpcGidrolockCraneCmd] = 0.000000
      }
      
      Прервать
    }
    
  }
  
Конец

Функция Gidrolock_CmdOpen
  Входные_Переменные
    Начало
      Цел16 gidrolockCraneId
    Конец
  Выходные_Переменные
    Начало
    Конец
Начало
  Константы
    Начало
    Конец
  Переменные
    Начало
    Конец
  Для iGidrolockCraneId[1, 2], iMdbCraneId[12, 15], iOpcGidrolockCraneCmd[8, 10], iOpcGidrolockCraneState[9, 11]
  {
    Если (iGidrolockCraneId = gidrolockCraneId)
    {
      Если ((рв[iOpcGidrolockCraneState] = 0.000000) | (рв[iOpcGidrolockCraneState] = 4.00000))
      {
        рв[iOpcGidrolockCraneCmd] = 1.00000
      }
      
      Прервать
    }
    
  }
  
Конец

Функция Gidrolock_GetStateById
  Входные_Переменные
    Начало
      Цел16 gidrolockCraneId
    Конец
  Выходные_Переменные
    Начало
      Цел16 gidrolockCraneState
    Конец
Начало
  Константы
    Начало
    Конец
  Переменные
    Начало
    Конец
  Для iGidrolockCraneId[1, 2], iOpcGidrolockCraneState[9, 11]
  {
    Если (iGidrolockCraneId = gidrolockCraneId)
    {
      gidrolockCraneState = вц(рв[iOpcGidrolockCraneState])
      Прервать
    }
    
  }
  
Конец

Функция Gidrolock_IsClosed
  Входные_Переменные
    Начало
      Цел16 gidrolockCraneId
    Конец
  Выходные_Переменные
    Начало
      Лог isClosedCrane
    Конец
Начало
  Константы
    Начало
    Конец
  Переменные
    Начало
      Вещ32 craneState
    Конец
  Для iGidrolockCraneId[1, 2], iOpcGidrolockCraneState[9, 11]
  {
    Если (iGidrolockCraneId = gidrolockCraneId)
    {
      craneState = рв[iOpcGidrolockCraneState]
      isClosedCrane = (craneState = 0.000000)
      Прервать
    }
    
  }
  
Конец

Функция Gidrolock_IsOpened
  Входные_Переменные
    Начало
      Цел16 gidrolockCraneId
    Конец
  Выходные_Переменные
    Начало
      Лог isOpenedCrane
    Конец
Начало
  Константы
    Начало
    Конец
  Переменные
    Начало
      Вещ32 craneState
    Конец
  Для iGidrolockCraneId[1, 2], iOpcGidrolockCraneState[9, 11]
  {
    Если (iGidrolockCraneId = gidrolockCraneId)
    {
      craneState = рв[iOpcGidrolockCraneState]
      isOpenedCrane = (craneState = 1.00000)
      Прервать
    }
    
  }
  
Конец

Функция Gidrolock_IsErrorInCloses
  Входные_Переменные
    Начало
      Цел16 gidrolockCraneId
    Конец
  Выходные_Переменные
    Начало
      Лог isErrorInCloses
    Конец
Начало
  Константы
    Начало
    Конец
  Переменные
    Начало
    Конец
  Для iGidrolockCraneId[1, 2], iOpcGidrolockCraneState[9, 11], iMdbCraneId[12, 15]
  {
    Если (iGidrolockCraneId = gidrolockCraneId)
    {
      isErrorInCloses = ((рв[iOpcGidrolockCraneState] = 3.00000) & (!пл[iMdbCraneId]))
      Прервать
    }
    
  }
  
Конец

Функция Gidrolock_IsErrorInOpens
  Входные_Переменные
    Начало
      Цел16 gidrolockCraneId
    Конец
  Выходные_Переменные
    Начало
      Лог isErrorInOpens
    Конец
Начало
  Константы
    Начало
    Конец
  Переменные
    Начало
    Конец
  Для iGidrolockCraneId[1, 2], iOpcGidrolockCraneState[9, 11], iMdbCraneId[12, 15]
  {
    Если (iGidrolockCraneId = gidrolockCraneId)
    {
      isErrorInOpens = ((рв[iOpcGidrolockCraneState] = 3.00000) & пл[iMdbCraneId])
      Прервать
    }
    
  }
  
Конец

